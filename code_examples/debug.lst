
Processor: GUS16 v6

; Label listing -----------------------------------

0006   (    6)   CPUMODEL
0020   (   32)   IOBASE
0020   (   32)   IRQEN
0021   (   33)   PFLAGS
0021   (   33)   PWM
0022   (   34)   UARTDAT
0023   (   35)   TIMER
0024   (   36)   GPO
0026   (   38)   FLAG0
0027   (   39)   PC0
0001   (    1)   IRQRXEN
0002   (    2)   IRQTXEN
0004   (    4)   IRQTIMEN
0008   (    8)   IRQPWMEN
0010   (   16)   IRQSSTEPEN
0001   (    1)   UARTDV
0002   (    2)   UARTRDY
0004   (    4)   UARTOVER
0008   (    8)   UARTFER
0010   (   16)   PWMF
8000   (32768)   TIMOV
FC00   (64512)   PINIT
FC00   (64512)   SSTEPIRQ
FAEF   (64239)   JMPIRQ
FC05   (64517)   DB00
FC0E   (64526)   DB001
FC11   (64529)   DB002
FC12   (64530)   DB01
FC22   (64546)   DB02
FC24   (64548)   DB03
FC2B   (64555)   DBUPD
FC2C   (64556)   DB04
FC3A   (64570)   DB2
FC3E   (64574)   DB3
FC43   (64579)   DB4
FC5A   (64602)   DB5
FC67   (64615)   DBPROM
FC68   (64616)   DBKEY
FC6E   (64622)   DB51
FC73   (64627)   DB10
FC7A   (64634)   DB15
FC7D   (64637)   DB20
FC82   (64642)   DB30
FC85   (64645)   DB31
FC8B   (64651)   DB35
FC8F   (64655)   DB40
FC98   (64664)   DB45
FC9B   (64667)   DB44
FC9E   (64670)   DB50
FCA5   (64677)   DB60
FCAB   (64683)   DB70
FCB1   (64689)   IEND0
FCB2   (64690)   IEND
FCB5   (64693)   IEND2
FCBB   (64699)   MSGCLS
FCBF   (64703)   MSGFLG
FCCD   (64717)   FLGTBL
FCD1   (64721)   MSGPOS
FCD3   (64723)   MSGREG
FCD6   (64726)   MSGBRK
FCDE   (64734)   MSGEBRK
FCE8   (64744)   MSGPAUSE
FCED   (64749)   DISABLK
FCF0   (64752)   DISA1
FCFE   (64766)   DISA2
FD00   (64768)   DISA3
FD08   (64776)   DISASSEMBLE
FD21   (64801)   DIS01
FD26   (64806)   DIS1
FD41   (64833)   DIS13
FD44   (64836)   DIS11
FD51   (64849)   DIS12
FD59   (64857)   DIS2
FD68   (64872)   DIS20
FD70   (64880)   DIS21
FD7B   (64891)   DIS22
FD81   (64897)   DIS221
FD8B   (64907)   DIS23
FDA3   (64931)   DIS24
FDA3   (64931)   DIS3
FDB4   (64948)   DIS4
FDC7   (64967)   DIS99
FDCD   (64973)   PRNEMO
FDE3   (64995)   PRREG
FDEA   (65002)   NMTAB
FE34   (65076)   GETHEX
FE3C   (65084)   GHEX1
FE45   (65093)   GHEX2
FE4C   (65100)   GHEX3
FE51   (65105)   GHEX9
FE57   (65111)   GHEX95
FE5B   (65115)   MDUMP
FE5E   (65118)   MD1
FE65   (65125)   MD2
FE71   (65137)   MD3
FE84   (65156)   GOODCH
FE89   (65161)   GOCH1
FE8A   (65162)   GOCH2
FE8B   (65163)   PUTSBE
FE8C   (65164)   PUTSBE1
FE97   (65175)   PUTSBE3
FE98   (65176)   PUTCH
FE9B   (65179)   PRTDEC
FEA0   (65184)   PRTD0
FEA2   (65186)   PRTD1
FEA8   (65192)   PRTD2
FEA9   (65193)   PRTD5
FEAE   (65198)   PRTD6
FEB4   (65204)   PRTD3
FEB9   (65209)   PRTD9
FEBD   (65213)   PRTHEX8
FEC4   (65220)   PRTHEX
FEC9   (65225)   PTX1
FECF   (65231)   PTX2
FED7   (65239)   BVAR
FED7   (65239)   LDPCF
FED8   (65240)   TRACE
FED9   (65241)   BREAK
FEDA   (65242)   RUTSP
FEDB   (65243)   CADDR
FEDC   (65244)   MSGHELP
FF56   (65366)   TXTLOAD
FF63   (65379)   PSTART
FF63   (65379)   START
FF68   (65384)   BUC1
FF72   (65394)   BUC2
FF85   (65413)   GETW
FF8A   (65418)   PEND

; Program listing ----------------------------------

0000  -         ;---------------------------------------------------------------------
0000  -         ; PROGRAMMING CONVENTIONS:
0000  -         ; STRICT:
0000  -         ; - CORE REVISION 6 INSTRUCTION SET
0000  -         ; - R7 IS THE STACK POINTER	(SOFTWARE)
0000  -         ; - R6 HOLDS THE RETURN ADDRESS FOR SUBROUTINES (JAL)
0000  -         ; - FULL-DESCENDING STACK
0000  -         ; LOOSE GUIDELINES:
0000  -         ; - R0, R1, R2 ARE USED FOR ARGUMENTS / RESULTS
0000  -         ;              VALUES MAY CHANGE ON SUBROUTINE CALLS
0000  -         ; - R3, R4, R5 ARE USED FOR LOCAL VARIABLES
0000  -         ;              VALUES ARE PRESERVED ON SUBROUTINE CALLS
0000  -         ;---------------------------------------------------------------------
0000  -         ;---------------------------------------------------------------------
0000  -         ;----------- I/O MAP  -----------
0000  -         
0000  -         IOBASE=		0X20
0000  -         IRQEN=		0X20
0000  -         PFLAGS=		0X21
0000  -         PWM=		0X21
0000  -         UARTDAT=	0X22
0000  -         TIMER=		0X23
0000  -         GPO=		0X24
0000  -         
0000  -         FLAG0=		0X26	; USER-MODE FLAGS
0000  -         PC0=		0X27	; USER-MODE PC
0000  -         
0000  -         ;--------- INTERRUPT ENABLE --------
0000  -         IRQRXEN=	1
0000  -         IRQTXEN=	2
0000  -         IRQTIMEN=	4
0000  -         IRQPWMEN=	8
0000  -         IRQSSTEPEN=	16		; SINGLE-STEP IRQ
0000  -         ;--------- PFLAGS REGISTER BITS -----------
0000  -         UARTDV=		1		; DATA VALID (UART RX)
0000  -         UARTRDY=	2		; TRANSMITTER READY (UART TX)
0000  -         UARTOVER=	4		; RX OVERRUN ERROR
0000  -         UARTFER=	8		; RX FRAME ERROR
0000  -         PWMF=		16		; PWM INTERRUPT
0000  -         TIMOV=		0X8000	; TIMER OVERFLOW (SIGN BIT)
0000  -         
0000  -         ;--------------------------------------
0000  -         ;------- HEADER FOR BOOTLOADER --------
0000  -         ;--------------------------------------
0000  -         
0000  -         		ORG		0XFC00-4
FBFC  -         		
FBFC  -  4CFF   		WORD	0X4CFF			; MARK
FBFD  -  FC00   		WORD	PINIT			; DESTINATION ADDRESS
FBFE  -  038A   		WORD	PEND-PINIT		; SIZE (WORDS)
FBFF  -  FF63   		WORD	PSTART			; EXECUTION ADDRESS
FC00  -         
FC00  -         ;--------------------------------------
FC00  -         ;------------- CODE -----------------
FC00  -         ;--------------------------------------
FC00  -         PINIT:
FC00  -         
FC00  -         ;-------------------------------------------------------------------
FC00  -         ; INTERRUPTS
FC00  -         ;-------------------------------------------------------------------
FC00  -         		
FC00  -         SSTEPIRQ:
FC00  -         
FC00  -         JMPIRQ= ~(0X10110-SSTEPIRQ)		; OPCODE OF: JR FROM 0X110 TO HERE
FC00  -         
FC00  -  1F07   		SUBI	R7,7
FC01  -  69E1   		ST		(R7+1),R1
FC02  -  6BE3   		ST		(R7+3),R3
FC03  -  6CE4   		ST		(R7+4),R4
FC04  -  6DE5   		ST		(R7+5),R5
FC05  -         
FC05  -  5520   DB00:	LDI		R5,IOBASE
FC06  -  5BE0   		LDPC	R3
FC07  -  FED7   		WORD	BVAR
FC08  -         		
FC08  -  64A7   		LD		R4,(R5+PC0-IOBASE)		; R4: PC NORMAL MODE
FC09  -  61A1   		LD		R1,(R5+PFLAGS-IOBASE)
FC0A  -  3140   		ANDI	R1,0X40					; CTRL-C FLAG SET?
FC0B  -  8002   		JZ		DB001
FC0C  -  61A2   		LD		R1,(R5+UARTDAT-IOBASE)	; FLUSH RX
FC0D  -  F003   		JR		DB002
FC0E  -  6162   DB001:	LD		R1,(R3+BREAK-BVAR)	; IF(BREAK==PC) SET_TRACE
FC0F  -  0986   		XOR		R1,R4,R1			; CHECK FOR BREAKPOINT
FC10  -  9001   		JNZ		DB01
FC11  -  6D61   DB002:	ST		(R3+TRACE-BVAR),R5	
FC12  -         DB01:		
FC12  -  6161   		LD		R1,(R3+TRACE-BVAR)	; IF (!TRACE) RETURN
FC13  -  80A1   		JZ		IEND2
FC14  -  68E0   		ST		(R7+0),R0			; SAVE REST OF REGISTERS
FC15  -  6AE2   		ST		(R7+2),R2
FC16  -  6EE6   		ST		(R7+6),R6
FC17  -  D00C   		JPL		DB03				; IF (TRACE<0) WAIT FOR RETURN (JIND R6)
FC18  -  6080   		LD		R0,(R4)
FC19  -  5AE0   		LDPC	R2
FC1A  -  58FA   		JIND R6
FC1B  -  080A   		XOR		R0,R0,R2
FC1C  -  9095   		JNZ		IEND
FC1D  -  3101   		ANDI	R1,1
FC1E  -  8003   		JZ		DB02
FC1F  -  6163   		LD		R1,(R3+RUTSP-BVAR)	; HIGHER STACK LEVEL?
FC20  -  013D   		SUB		R1,R1,R7
FC21  -  D090   		JPL		IEND
FC22  -  5102   DB02:	LDI		R1,2
FC23  -  6961   		ST		(R3+TRACE-BVAR),R1	; TRACE POSITIVE
FC24  -         
FC24  -  60A1   DB03:	LD		R0,(R5+PFLAGS-IOBASE)
FC25  -  3020   		ANDI	R0,0X20				; SCREEN DIRTY?
FC26  -  8005   		JZ		DB04
FC27  -  58E0   		LDPC	R0
FC28  -  FCE8   		WORD	MSGPAUSE
FC29  -  7261   		JAL		PUTSBE
FC2A  -  60A2   		LD		R0,(R5+UARTDAT-IOBASE)	; GETCH
FC2B  -         
FC2B  -  64A7   DBUPD:	LD		R4,(R5+PC0-IOBASE)
FC2C  -  5100   DB04:	LDI		R1,0
FC2D  -  6960   		ST		(R3+LDPCF-BVAR),R1	; CLEAR LDPC FLAG
FC2E  -         
FC2E  -  58E0   		LDPC	R0					; CLEAR SCREEN
FC2F  -  FCBB   		WORD	MSGCLS ;+2
FC30  -  725A   		JAL		PUTSBE
FC31  -         		
FC31  -         		; PRINT CODE @ PC0
FC31  -  70BB   		JAL		DISABLK		
FC32  -         		; PRINT FLAGS0
FC32  -  58E0   		LDPC	R0
FC33  -  FCBF   		WORD	MSGFLG
FC34  -  7256   		JAL		PUTSBE
FC35  -  64A6   		LD		R4,(R5+FLAG0-IOBASE)
FC36  -  5C30   		RORI	R4,R4,4
FC37  -  5304   		LDI		R3,4
FC38  -  5AE0   		LDPC	R2
FC39  -  FCCD   		WORD	FLGTBL
FC3A  -  505F   DB2:	LDI		R0,'_'
FC3B  -  0490   		ADD		R4,R4,R4		
FC3C  -  B001   		JNC		DB3
FC3D  -  6040   		LD		R0,(R2)	
FC3E  -  68A2   DB3:	ST		(R5+UARTDAT-IOBASE),R0
FC3F  -  1201   		ADDI	R2,1
FC40  -  1B01   		SUBI	R3,1
FC41  -  9FF8   		JNZ		DB2
FC42  -         		
FC42  -         		; PRINT REGISTERS
FC42  -  5300   		LDI		R3,0
FC43  -  58E0   DB4:	LDPC	R0
FC44  -  FCD1   		WORD	MSGPOS
FC45  -  7245   		JAL		PUTSBE
FC46  -  5003   		LDI		R0,3
FC47  -  0060   		ADD		R0,R3,R0
FC48  -  7252   		JAL		PRTDEC
FC49  -  58E0   		LDPC	R0
FC4A  -  FCD3   		WORD	MSGREG
FC4B  -  723F   		JAL		PUTSBE
FC4C  -  5030   		LDI		R0,'0'
FC4D  -  0060   		ADD		R0,R3,R0
FC4E  -  68A2   		ST		(R5+UARTDAT-IOBASE),R0
FC4F  -  503A   		LDI		R0,':'
FC50  -  68A2   		ST		(R5+UARTDAT-IOBASE),R0
FC51  -  5020   		LDI		R0,' '
FC52  -  68A2   		ST		(R5+UARTDAT-IOBASE),R0
FC53  -  086D   		OR		R0,R3,R3
FC54  -  001C   		ADD		R0,R0,R7
FC55  -  6000   		LD		R0,(R0)
FC56  -  4B07   		CMPI	R3,7
FC57  -  9002   		JNZ		DB5
FC58  -  5007   		LDI		R0,7
FC59  -  001C   		ADD		R0,R0,R7
FC5A  -  7269   DB5:	JAL		PRTHEX
FC5B  -  1301   		ADDI	R3,1
FC5C  -  4B08   		CMPI	R3,8
FC5D  -  9FE5   		JNZ		DB4
FC5E  -         
FC5E  -         		; PRINT BRK
FC5E  -  58E0   		LDPC	R0
FC5F  -  FCD6   		WORD	MSGBRK
FC60  -  722A   		JAL		PUTSBE
FC61  -  58E0   		LDPC	R0
FC62  -  FED7   		WORD	BVAR
FC63  -  6002   		LD		R0,(R0+BREAK-BVAR)
FC64  -  725F   		JAL		PRTHEX
FC65  -  58E0   		LDPC	R0
FC66  -  FCDE   		WORD	MSGEBRK
FC67  -  7223   DBPROM:	JAL		PUTSBE
FC68  -         		;------------------
FC68  -         		; USER COMMANDS
FC68  -         		;------------------
FC68  -  60A2   DBKEY:	LD		R0,(R5+UARTDAT-IOBASE)
FC69  -  4863   		CMPI	R0,'c'		; CONTINUE
FC6A  -  9008   		JNZ		DB10
FC6B  -  60A0   		LD		R0,(R5)		; DISABLE SINGLE-STEP IRQ
FC6C  -  30EF   		ANDI	R0,0XEF
FC6D  -  68A0   		ST		(R5),R0
FC6E  -  58E0   DB51:	LDPC	R0
FC6F  -  FCBB   		WORD	MSGCLS
FC70  -  721A   		JAL		PUTSBE
FC71  -  68A6   		ST		(R5+FLAG0-IOBASE),R0	; CLEAR DIRTY
FC72  -  F03F   		JR		IEND
FC73  -         		
FC73  -  486E   DB10:	CMPI	R0,'n'		; BREAK ON NEXT INSTR
FC74  -  9008   		JNZ		DB20
FC75  -  60A7   		LD		R0,(R5+PC0-IOBASE)
FC76  -  1001   		ADDI	R0,1
FC77  -  5BE0   		LDPC	R3
FC78  -  FED7   		WORD	BVAR
FC79  -  6862   		ST		(R3+BREAK-BVAR),R0
FC7A  -  5000   DB15:	LDI		R0,0
FC7B  -  6861   		ST		(R3+TRACE-BVAR),R0
FC7C  -  FFF1   		JR		DB51
FC7D  -         
FC7D  -  4865   DB20:	CMPI	R0,'e'		; EXEC WITH BREAKPOINT (SLOW)
FC7E  -  9003   		JNZ		DB30
FC7F  -  5BE0   		LDPC	R3
FC80  -  FED7   		WORD	BVAR
FC81  -  FFF8   		JR		DB15
FC82  -         		
FC82  -  4872   DB30:	CMPI	R0,'r'		; EXEC UNTIL RETURN
FC83  -  9007   		JNZ		DB35
FC84  -  5001   		LDI		R0,1
FC85  -  5BE0   DB31:	LDPC	R3
FC86  -  FED7   		WORD	BVAR
FC87  -  5801   		RORI	R0,R0,1
FC88  -  6861   		ST		(R3+TRACE-BVAR),R0	; TRACE<0 MEANS STOP ON RETURNS
FC89  -  6F63   		ST		(R3+RUTSP-BVAR),R7	; STACK LEVEL
FC8A  -  FFE3   		JR		DB51
FC8B  -  4852   DB35:	CMPI	R0,'R'	; EXEC UNTIL RETURN (HIGHER STACK)
FC8C  -  9002   		JNZ		DB40
FC8D  -  5003   		LDI		R0,3	; TRACE<0 && (TRACE&1) => STOP ON RETURNS IF STACK IS HIGHER
FC8E  -  FFF6   		JR		DB31
FC8F  -         
FC8F  -  4873   DB40:	CMPI	R0,'s'		; STEP
FC90  -  8020   		JZ		IEND0
FC91  -         		;CMPI	R0,'\n'
FC91  -         		;JZ		DB00
FC91  -  4820   		CMPI	R0,' '
FC92  -  8F72   		JZ		DB00
FC93  -         		
FC93  -  4864   		CMPI	R0,'d'		; DISASSEMBLE
FC94  -  9009   		JNZ		DB50
FC95  -  719E   		JAL		GETHEX
FC96  -  0C01   		OR		R4,R0,R0
FC97  -  7055   		JAL		DISABLK
FC98  -  5BE0   DB45:	LDPC	R3
FC99  -  FED7   		WORD	BVAR
FC9A  -  6C64   		ST		(R3+CADDR-BVAR),R4
FC9B  -  58E0   DB44:	LDPC	R0
FC9C  -  FCE2   		WORD	MSGEBRK+4
FC9D  -  FFC9   		JR		DBPROM
FC9E  -         			
FC9E  -  4862   DB50:	CMPI	R0,'b'		; SET BREAKPOINT
FC9F  -  9005   		JNZ		DB60
FCA0  -  7193   		JAL		GETHEX	
FCA1  -  5BE0   		LDPC	R3
FCA2  -  FED7   		WORD	BVAR
FCA3  -  6862   		ST		(R3+BREAK-BVAR),R0
FCA4  -  FF86   		JR		DBUPD
FCA5  -         
FCA5  -  486D   DB60:	CMPI	R0,'m'		; DUMP MEMORY
FCA6  -  9004   		JNZ		DB70
FCA7  -  718C   		JAL		GETHEX
FCA8  -  0C01   		OR		R4,R0,R0
FCA9  -  71B1   		JAL		MDUMP
FCAA  -  FFED   		JR		DB45
FCAB  -         		
FCAB  -  4868   DB70:	CMPI	R0,'h'		; PRINT HELP
FCAC  -  9FBB   		JNZ		DBKEY
FCAD  -  58E0   		LDPC	R0
FCAE  -  FEDC   		WORD	MSGHELP
FCAF  -  71DB   		JAL		PUTSBE
FCB0  -  FFEA   		JR		DB44
FCB1  -         
FCB1  -  68A6   IEND0:	ST		(R5+FLAG0-IOBASE),R0	; CLEAR DIRTY
FCB2  -  60E0   IEND:	LD		R0,(R7+0)
FCB3  -  62E2   		LD		R2,(R7+2)
FCB4  -  66E6   		LD		R6,(R7+6)
FCB5  -         
FCB5  -  61E1   IEND2:	LD		R1,(R7+1)
FCB6  -  63E3   		LD		R3,(R7+3)
FCB7  -  64E4   		LD		R4,(R7+4)
FCB8  -  65E5   		LD		R5,(R7+5)
FCB9  -  1707   		ADDI	R7,7
FCBA  -  58E3   		RETI
FCBB  -         
FCBB  -  1B5B   MSGCLS:	ASCZBE "\e[2J\e[H"
FCBC  -  324A   
FCBD  -  1B5B   
FCBE  -  4800   
FCBF  -  1B5B   MSGFLG:	ASCZBE "\e[1;32H<- PC\e[1;39HFlags: "
FCC0  -  313B   
FCC1  -  3332   
FCC2  -  483C   
FCC3  -  2D20   
FCC4  -  5043   
FCC5  -  1B5B   
FCC6  -  313B   
FCC7  -  3339   
FCC8  -  4846   
FCC9  -  6C61   
FCCA  -  6773   
FCCB  -  3A20   
FCCC  -  0000   
FCCD  -  0056   FLGTBL: WORD 'V'
FCCE  -  004E   		WORD 'N'
FCCF  -  0043   		WORD 'C'
FCD0  -  005A   		WORD 'Z'
FCD1  -  1B5B   MSGPOS:	ASCZBE "\e["
FCD2  -  0000   
FCD3  -  3B34   MSGREG:	ASCZBE ";42HR"
FCD4  -  3248   
FCD5  -  5200   
FCD6  -  1B5B   MSGBRK:	ASCZBE "\e[13;39HBreak: "
FCD7  -  3133   
FCD8  -  3B33   
FCD9  -  3948   
FCDA  -  4272   
FCDB  -  6561   
FCDC  -  6B3A   
FCDD  -  2000   
FCDE  -  1B5B   MSGEBRK:	ASCZBE "\e[22;01HhcesnrRbdm>"
FCDF  -  3232   
FCE0  -  3B30   
FCE1  -  3148   
FCE2  -  6863   
FCE3  -  6573   
FCE4  -  6E72   
FCE5  -  5262   
FCE6  -  646D   
FCE7  -  3E00   
FCE8  -  3C70   MSGPAUSE:	ASCZBE "<paused>"
FCE9  -  6175   
FCEA  -  7365   
FCEB  -  643E   
FCEC  -  0000   
FCED  -         
FCED  -         ;------------------------------------------------------
FCED  -         ; DISSASEMBLE 20 INSTR AT (R4)
FCED  -         DISABLK:
FCED  -  1F01   		SUBI	R7,1
FCEE  -  6EE0   		ST		(R7),R6
FCEF  -         
FCEF  -  5314   		LDI		R3,20
FCF0  -  0891   DISA1:	OR		R0,R4,R4
FCF1  -  71D2   		JAL		PRTHEX
FCF2  -  503A   		LDI		R0,':'
FCF3  -  68A2   		ST		(R5+UARTDAT-IOBASE),R0
FCF4  -  5020   		LDI		R0,' '
FCF5  -  68A2   		ST		(R5+UARTDAT-IOBASE),R0
FCF6  -  6080   		LD		R0,(R4)
FCF7  -  71CC   		JAL		PRTHEX
FCF8  -  59E0   		LDPC	R1
FCF9  -  FED7   		WORD	LDPCF
FCFA  -  6120   		LD		R1,(R1)
FCFB  -  9002   		JNZ		DISA2
FCFC  -  700B   		JAL		DISASSEMBLE
FCFD  -  F002   		JR		DISA3
FCFE  -  5000   DISA2:	LDI		R0,0
FCFF  -  6820   		ST		(R1),R0
FD00  -  500A   DISA3:	LDI		R0,'\n'
FD01  -  68A2   		ST		(R5+UARTDAT-IOBASE),R0
FD02  -  1401   		ADDI	R4,1
FD03  -  1B01   		SUBI	R3,1
FD04  -  9FEB   		JNZ		DISA1
FD05  -         
FD05  -  66E0   		LD		R6,(R7)
FD06  -  1701   		ADDI	R7,1
FD07  -  58FA   		JIND	R6
FD08  -         
FD08  -         ;------------------------------------------------------
FD08  -         ; DISSASEMBLE SINGLE INSTRUCTION AT (R4)
FD08  -         DISASSEMBLE:
FD08  -  1F04   		SUBI	R7,4
FD09  -  68E0   		ST		(R7+0),R0
FD0A  -  69E1   		ST		(R7+1),R1
FD0B  -  6DE2   		ST		(R7+2),R5
FD0C  -  6EE3   		ST		(R7+3),R6
FD0D  -         		
FD0D  -  5000   		LDI		R0,0
FD0E  -  59E0   		LDPC	R1
FD0F  -  FED7   		WORD	LDPCF
FD10  -  6820   		ST		(R1),R0
FD11  -  5020   		LDI		R0,' '
FD12  -  7185   		JAL		PUTCH
FD13  -  6080   		LD		R0,(R4)
FD14  -  5860   		RORI	R0,R0,12
FD15  -  300F   		ANDI	R0,0XF
FD16  -  4807   		CMPI	R0,7
FD17  -  C00E   		JMI		DIS1
FD18  -  1015   		ADDI	R0,28-7
FD19  -  70B3   		JAL		PRNEMO
FD1A  -  6180   		LD		R1,(R4)
FD1B  -  5DE0   		LDPC	R5
FD1C  -  0FFF   		WORD	0XFFF
FD1D  -  0DA4   		AND		R5,R5,R1
FD1E  -  5D74   		RORI	R5,R5,12
FD1F  -  D001   		JPL		DIS01
FD20  -  3D0F   		ORI		R5,0XF
FD21  -  5D34   DIS01:	RORI	R5,R5,4	
FD22  -  00B0   		ADD		R0,R5,R4
FD23  -  1001   		ADDI	R0,1
FD24  -  719F   		JAL		PRTHEX
FD25  -  F0A1   		JR		DIS99
FD26  -         
FD26  -  4806   DIS1:	CMPI	R0,6
FD27  -  9031   		JNZ		DIS2
FD28  -  6180   		LD		R1,(R4)
FD29  -  5847   		RORI	R0,R1,11
FD2A  -  3001   		ANDI	R0,1
FD2B  -  101A   		ADDI	R0,26
FD2C  -  70A0   		JAL		PRNEMO
FD2D  -  6080   		LD		R0,(R4)
FD2E  -  5860   		RORI	R0,R0,12
FD2F  -  C014   		JMI		DIS11
FD30  -  5860   		RORI	R0,R0,12	; LD
FD31  -  70B1   		JAL		PRREG
FD32  -  502C   		LDI		R0,','
FD33  -  7164   		JAL		PUTCH
FD34  -  5028   		LDI		R0,'('
FD35  -  7162   		JAL		PUTCH
FD36  -  6080   		LD		R0,(R4)
FD37  -  5821   		RORI	R0,R0,5
FD38  -  70AA   		JAL		PRREG
FD39  -  6080   		LD		R0,(R4)
FD3A  -  301F   		ANDI	R0,31
FD3B  -  8005   		JZ		DIS13
FD3C  -  502B   		LDI		R0,'+'
FD3D  -  715A   		JAL		PUTCH
FD3E  -  6080   		LD		R0,(R4)
FD3F  -  301F   		ANDI	R0,31
FD40  -  715A   		JAL		PRTDEC
FD41  -  5029   DIS13:	LDI		R0,')'
FD42  -  7155   		JAL		PUTCH
FD43  -  F083   		JR		DIS99
FD44  -  5028   DIS11:	LDI		R0,'('	; ST
FD45  -  7152   		JAL		PUTCH
FD46  -  6080   		LD		R0,(R4)
FD47  -  5821   		RORI	R0,R0,5
FD48  -  709A   		JAL		PRREG
FD49  -  6080   		LD		R0,(R4)
FD4A  -  301F   		ANDI	R0,31
FD4B  -  8005   		JZ		DIS12
FD4C  -  502B   		LDI		R0,'+'
FD4D  -  714A   		JAL		PUTCH
FD4E  -  6080   		LD		R0,(R4)
FD4F  -  301F   		ANDI	R0,31
FD50  -  714A   		JAL		PRTDEC		
FD51  -  5029   DIS12:	LDI		R0,')'
FD52  -  7145   		JAL		PUTCH
FD53  -  502C   		LDI		R0,','
FD54  -  7143   		JAL		PUTCH
FD55  -  6080   		LD		R0,(R4)
FD56  -  5840   		RORI	R0,R0,8
FD57  -  708B   		JAL		PRREG
FD58  -  F06E   		JR		DIS99
FD59  -         		
FD59  -  4805   DIS2:	CMPI	R0,5
FD5A  -  9048   		JNZ		DIS3
FD5B  -  6080   		LD		R0,(R4)
FD5C  -  5903   		RORI	R1,R0,3
FD5D  -  311C   		ANDI	R1,0X1C
FD5E  -  5A22   		RORI	R2,R0,6
FD5F  -  3220   		ANDI	R2,0X20
FD60  -  3003   		ANDI	R0,3
FD61  -  0805   		OR		R0,R0,R1
FD62  -  0809   		OR		R0,R0,R2
FD63  -  483F   		CMPI	R0,0X3F
FD64  -  9003   		JNZ		DIS20
FD65  -  5019   		LDI		R0,25		; RETI
FD66  -  7066   		JAL		PRNEMO
FD67  -  F05F   		JR		DIS99
FD68  -  483E   DIS20:	CMPI	R0,0X3E
FD69  -  9006   		JNZ		DIS21
FD6A  -  5018   		LDI		R0,24		; JIND
FD6B  -  7061   		JAL		PRNEMO
FD6C  -  6080   		LD		R0,(R4)
FD6D  -  5802   		RORI	R0,R0,2
FD6E  -  7074   		JAL		PRREG
FD6F  -  F057   		JR		DIS99
FD70  -  483C   DIS21:	CMPI	R0,0X3C
FD71  -  9009   		JNZ		DIS22
FD72  -  5017   		LDI		R0,23		; LDPC
FD73  -  7059   		JAL		PRNEMO
FD74  -  6080   		LD		R0,(R4)
FD75  -  5840   		RORI	R0,R0,8
FD76  -  706C   		JAL		PRREG
FD77  -  58E0   		LDPC	R0
FD78  -  FED7   		WORD	LDPCF
FD79  -  6800   		ST		(R0),R0
FD7A  -  F04C   		JR		DIS99
FD7B  -  4830   DIS22:	CMPI	R0,0X30
FD7C  -  C00E   		JMI		DIS23
FD7D  -  181E   		SUBI	R0,48-18	; RORC - NEG
FD7E  -  4816   		CMPI	R0,22
FD7F  -  C001   		JMI		DIS221
FD80  -  1801   		SUBI	R0,1
FD81  -  704B   DIS221:	JAL		PRNEMO	
FD82  -  6080   		LD		R0,(R4)
FD83  -  5840   		RORI	R0,R0,8
FD84  -  705E   		JAL		PRREG
FD85  -  502C   		LDI		R0,','
FD86  -  7111   		JAL		PUTCH
FD87  -  6080   		LD		R0,(R4)
FD88  -  5802   		RORI	R0,R0,2
FD89  -  7059   		JAL		PRREG	
FD8A  -  F03C   		JR		DIS99
FD8B  -  5820   DIS23:	RORI	R0,R0,4
FD8C  -  3003   		ANDI	R0,3
FD8D  -  4802   		CMPI	R0,2
FD8E  -  9014   		JNZ		DIS24
FD8F  -  5011   		LDI		R0,17		; RORI
FD90  -  703C   		JAL		PRNEMO
FD91  -  6080   		LD		R0,(R4)
FD92  -  5840   		RORI	R0,R0,8
FD93  -  704F   		JAL		PRREG
FD94  -  502C   		LDI		R0,','
FD95  -  7102   		JAL		PUTCH
FD96  -  6080   		LD		R0,(R4)
FD97  -  5802   		RORI	R0,R0,2
FD98  -  704A   		JAL		PRREG
FD99  -  502C   		LDI		R0,','
FD9A  -  70FD   		JAL		PUTCH
FD9B  -  6080   		LD		R0,(R4)
FD9C  -  5903   		RORI	R1,R0,3
FD9D  -  3003   		ANDI	R0,3
FD9E  -  310C   		ANDI	R1,0XC
FD9F  -  0805   		OR		R0,R0,R1
FDA0  -  520A   		LDI		R2,10
FDA1  -  70F9   		JAL		PRTDEC
FDA2  -  F024   		JR		DIS99
FDA3  -         DIS24:	; LDI
FDA3  -  6080   DIS3:	LD		R0,(R4)		; IMMEDIATE INSTRS
FDA4  -  5843   		RORI	R0,R0,11
FDA5  -  300F   		ANDI	R0,0XF
FDA6  -  1802   		SUBI	R0,2
FDA7  -  C00C   		JMI		DIS4
FDA8  -  1008   		ADDI	R0,8
FDA9  -  7023   		JAL		PRNEMO
FDAA  -  6080   		LD		R0,(R4)
FDAB  -  5840   		RORI	R0,R0,8
FDAC  -  7036   		JAL		PRREG
FDAD  -  502C   		LDI		R0,','
FDAE  -  70E9   		JAL		PUTCH
FDAF  -  6080   		LD		R0,(R4)
FDB0  -  30FF   		ANDI	R0,0XFF
FDB1  -  520A   		LDI		R2,10
FDB2  -  710A   		JAL		PRTHEX8
FDB3  -  F013   		JR		DIS99
FDB4  -         
FDB4  -  6080   DIS4:	LD		R0,(R4)		; 3-OPERAND INSTRS
FDB5  -  5941   		RORI	R1,R0,9
FDB6  -  3003   		ANDI	R0,3
FDB7  -  3104   		ANDI	R1,4
FDB8  -  0805   		OR		R0,R0,R1
FDB9  -  7013   		JAL		PRNEMO
FDBA  -  6080   		LD		R0,(R4)
FDBB  -  5840   		RORI	R0,R0,8
FDBC  -  7026   		JAL		PRREG
FDBD  -  502C   		LDI		R0,','
FDBE  -  70D9   		JAL		PUTCH
FDBF  -  6080   		LD		R0,(R4)
FDC0  -  5821   		RORI	R0,R0,5
FDC1  -  7021   		JAL		PRREG
FDC2  -  502C   		LDI		R0,','
FDC3  -  70D4   		JAL		PUTCH
FDC4  -  6080   		LD		R0,(R4)
FDC5  -  5802   		RORI	R0,R0,2
FDC6  -  701C   		JAL		PRREG
FDC7  -         		
FDC7  -         
FDC7  -         DIS99:	
FDC7  -  60E0   		LD		R0,(R7+0)
FDC8  -  61E1   		LD		R1,(R7+1)
FDC9  -  65E2   		LD		R5,(R7+2)
FDCA  -  66E3   		LD		R6,(R7+3)
FDCB  -  1704   		ADDI	R7,4
FDCC  -  58FA   		JIND	R6
FDCD  -         		
FDCD  -         
FDCD  -  5122   PRNEMO:	LDI		R1,UARTDAT
FDCE  -  5520   		LDI		R5,' '
FDCF  -  6D20   		ST		(R1),R5
FDD0  -  6D20   		ST		(R1),R5		
FDD1  -  5DE0   		LDPC	R5
FDD2  -  FDEA   		WORD	NMTAB
FDD3  -  0000   		ADD		R0,R0,R0
FDD4  -  05A0   		ADD		R5,R5,R0
FDD5  -  60A0   		LD		R0,(R5)
FDD6  -  5840   		RORI	R0,R0,8
FDD7  -  6820   		ST		(R1),R0
FDD8  -  5840   		RORI	R0,R0,8
FDD9  -  6820   		ST		(R1),R0
FDDA  -  60A1   		LD		R0,(R5+1)
FDDB  -  5840   		RORI	R0,R0,8
FDDC  -  6820   		ST		(R1),R0
FDDD  -  5840   		RORI	R0,R0,8
FDDE  -  6820   		ST		(R1),R0
FDDF  -  5520   		LDI		R5,' '
FDE0  -  6D20   		ST		(R1),R5
FDE1  -  6D20   		ST		(R1),R5
FDE2  -  58FA   		JIND	R6
FDE3  -         
FDE3  -  5122   PRREG:	LDI		R1,UARTDAT
FDE4  -  5252   		LDI		R2,'R'
FDE5  -  6A20   		ST		(R1),R2
FDE6  -  3007   		ANDI	R0,7
FDE7  -  1030   		ADDI	R0,'0'
FDE8  -  6820   		ST		(R1),R0
FDE9  -  58FA   		JIND	R6
FDEA  -         		
FDEA  -         
FDEA  -         				
FDEA  -  4144   NMTAB:	WORD ('A'<<8)|'D'	; 0: ADD
FDEB  -  4420   		WORD ('D'<<8)|' '
FDEC  -  5355   		WORD ('S'<<8)|'U'	; 1: SUB
FDED  -  4220   		WORD ('B'<<8)|' '				
FDEE  -  4144   		WORD ('A'<<8)|'D'	; 2: ADC
FDEF  -  4320   		WORD ('C'<<8)|' '				
FDF0  -  5342   		WORD ('S'<<8)|'B'	; 3: SBC
FDF1  -  4320   		WORD ('C'<<8)|' '				
FDF2  -  414E   		WORD ('A'<<8)|'N'	; 4: AND
FDF3  -  4420   		WORD ('D'<<8)|' '				
FDF4  -  4F52   		WORD ('O'<<8)|'R'	; 5: OR
FDF5  -  2020   		WORD (' '<<8)|' '				
FDF6  -  584F   		WORD ('X'<<8)|'O'	; 6: XOR
FDF7  -  5220   		WORD ('R'<<8)|' '				
FDF8  -  4249   		WORD ('B'<<8)|'I'	; 7: BIC
FDF9  -  4320   		WORD ('C'<<8)|' '				
FDFA  -  4144   		WORD ('A'<<8)|'D'	; 8: ADDI
FDFB  -  4449   		WORD ('D'<<8)|'I'				
FDFC  -  5355   		WORD ('S'<<8)|'U'	; 9: SUBI
FDFD  -  4249   		WORD ('B'<<8)|'I'				
FDFE  -  4144   		WORD ('A'<<8)|'D'	; 10: ADCI
FDFF  -  4349   		WORD ('C'<<8)|'I'				
FE00  -  5342   		WORD ('S'<<8)|'B'	; 11: SBCI
FE01  -  4349   		WORD ('C'<<8)|'I'				
FE02  -  414E   		WORD ('A'<<8)|'N'	; 12: ANDI
FE03  -  4449   		WORD ('D'<<8)|'I'				
FE04  -  4F52   		WORD ('O'<<8)|'R'	; 13: ORI
FE05  -  4920   		WORD ('I'<<8)|' '				
FE06  -  584F   		WORD ('X'<<8)|'O'	; 14: XORI
FE07  -  5249   		WORD ('R'<<8)|'I'				
FE08  -  434D   		WORD ('C'<<8)|'M'	; 15: CMPI
FE09  -  5049   		WORD ('P'<<8)|'I'				
FE0A  -  4C44   		WORD ('L'<<8)|'D'	; 16: LDI
FE0B  -  4920   		WORD ('I'<<8)|' '				
FE0C  -  524F   		WORD ('R'<<8)|'O'	; 17: RORI
FE0D  -  5249   		WORD ('R'<<8)|'I'				
FE0E  -  524F   		WORD ('R'<<8)|'O'	; 18: RORC
FE0F  -  5243   		WORD ('R'<<8)|'C'				
FE10  -  5348   		WORD ('S'<<8)|'H'	; 19: SHR
FE11  -  5220   		WORD ('R'<<8)|' '				
FE12  -  5348   		WORD ('S'<<8)|'H'	; 20: SHRA
FE13  -  5241   		WORD ('R'<<8)|'A'				
FE14  -  4E4F   		WORD ('N'<<8)|'O'	; 21: NOT
FE15  -  5420   		WORD ('T'<<8)|' '				
FE16  -  4E45   		WORD ('N'<<8)|'E'	; 22: NEG
FE17  -  4720   		WORD ('G'<<8)|' '				
FE18  -  4C44   		WORD ('L'<<8)|'D'	; 23: LDPC
FE19  -  5043   		WORD ('P'<<8)|'C'				
FE1A  -  4A49   		WORD ('J'<<8)|'I'	; 24: JIND
FE1B  -  4E44   		WORD ('N'<<8)|'D'				
FE1C  -  5245   		WORD ('R'<<8)|'E'	; 25: RETI
FE1D  -  5449   		WORD ('T'<<8)|'I'				
FE1E  -  4C44   		WORD ('L'<<8)|'D'	; 26: LD
FE1F  -  2020   		WORD (' '<<8)|' '				
FE20  -  5354   		WORD ('S'<<8)|'T'	; 27: ST
FE21  -  2020   		WORD (' '<<8)|' '				
FE22  -  4A41   		WORD ('J'<<8)|'A'	; 28: JAL
FE23  -  4C20   		WORD ('L'<<8)|' '				
FE24  -  4A5A   		WORD ('J'<<8)|'Z'	; 29: JZ
FE25  -  2020   		WORD (' '<<8)|' '				
FE26  -  4A4E   		WORD ('J'<<8)|'N'	; 30: JNZ
FE27  -  5A20   		WORD ('Z'<<8)|' '				
FE28  -  4A43   		WORD ('J'<<8)|'C'	; 31: JC
FE29  -  2020   		WORD (' '<<8)|' '				
FE2A  -  4A4E   		WORD ('J'<<8)|'N'	; 32: JNC
FE2B  -  4320   		WORD ('C'<<8)|' '				
FE2C  -  4A4D   		WORD ('J'<<8)|'M'	; 33: JMI
FE2D  -  4920   		WORD ('I'<<8)|' '				
FE2E  -  4A50   		WORD ('J'<<8)|'P'	; 34: JPL
FE2F  -  4C20   		WORD ('L'<<8)|' '				
FE30  -  4A56   		WORD ('J'<<8)|'V'	; 35: JV
FE31  -  2020   		WORD (' '<<8)|' '		
FE32  -  4A52   		WORD ('J'<<8)|'R'	; 36: JR
FE33  -  2020   		WORD (' '<<8)|' '				
FE34  -         
FE34  -         ;-------------------------------------------------------------------
FE34  -         ; 	GET AN HEX VALUE FROM UART, WITH EDITING
FE34  -         ;     CADDR HOLDS THE INITIAL VALUE
FE34  -         ;     R0 CONTENTS ARE PRINTED AS CHAR IN FRONT TO THE HEX VALUE
FE34  -         ;     R0 RETURNS THE EDITED VALUE
FE34  -         ;   HEX DIGITS ARE SHIFTED TO THE LEFT UNTIL AN NEW-LINE IS RECEIVED
FE34  -  1F01   GETHEX:	SUBI	R7,1
FE35  -  6EE0   		ST		(R7),R6
FE36  -  7061   		JAL		PUTCH
FE37  -  5020   		LDI		R0,' '
FE38  -  705F   		JAL		PUTCH
FE39  -  5BE0   		LDPC	R3
FE3A  -  FED7   		WORD	BVAR
FE3B  -  6064   		LD		R0,(R3+CADDR-BVAR)
FE3C  -  7087   GHEX1:	JAL		PRTHEX
FE3D  -  61A2   		LD		R1,(R5+UARTDAT-IOBASE)
FE3E  -  490A   		CMPI	R1,'\n'
FE3F  -  8017   		JZ		GHEX95
FE40  -  4930   		CMPI	R1,'0'
FE41  -  C00F   		JMI		GHEX9
FE42  -  4960   		CMPI	R1,'a'-1
FE43  -  C001   		JMI		GHEX2
FE44  -  1920   		SUBI	R1,32
FE45  -  493A   GHEX2:	CMPI	R1,'9'+1
FE46  -  C005   		JMI		GHEX3
FE47  -  4940   		CMPI	R1,'A'-1
FE48  -  C008   		JMI		GHEX9
FE49  -  4947   		CMPI	R1,'F'+1
FE4A  -  D006   		JPL		GHEX9
FE4B  -  1907   		SUBI	R1,7
FE4C  -  1930   GHEX3:	SUBI	R1,'0'
FE4D  -  5860   		RORI	R0,R0,12
FE4E  -  380F   		ORI		R0,0XF
FE4F  -  400F   		XORI	R0,0XF
FE50  -  0004   		ADD		R0,R0,R1
FE51  -         		
FE51  -  5108   GHEX9:	LDI		R1,'\b'
FE52  -  69A2   		ST		(R5+UARTDAT-IOBASE),R1
FE53  -  69A2   		ST		(R5+UARTDAT-IOBASE),R1
FE54  -  69A2   		ST		(R5+UARTDAT-IOBASE),R1
FE55  -  69A2   		ST		(R5+UARTDAT-IOBASE),R1
FE56  -  FFE5   		JR		GHEX1			
FE57  -         
FE57  -  69A2   GHEX95:	ST		(R5+UARTDAT-IOBASE),R1
FE58  -  66E0   		LD		R6,(R7)
FE59  -  1701   		ADDI	R7,1
FE5A  -  58FA   		JIND	R6
FE5B  -         ;-------------------------------------------------------------------
FE5B  -         ; HEX DUMP OF THE MEMORY BLOCK POINTED BY R4
FE5B  -  1F01   MDUMP:	SUBI	R7,1
FE5C  -  6EE0   		ST		(R7),R6
FE5D  -  5310   		LDI		R3,16
FE5E  -  0891   MD1:	OR		R0,R4,R4
FE5F  -  7064   		JAL		PRTHEX			; ADDRESS
FE60  -  513A   		LDI		R1,':'
FE61  -  69A2   		ST		(R5+UARTDAT-IOBASE),R1
FE62  -  5120   		LDI		R1,' '
FE63  -  69A2   		ST		(R5+UARTDAT-IOBASE),R1
FE64  -  5208   		LDI		R2,8
FE65  -  6080   MD2:	LD		R0,(R4)			; DATA
FE66  -  705D   		JAL		PRTHEX
FE67  -  5120   		LDI		R1,' '
FE68  -  69A2   		ST		(R5+UARTDAT-IOBASE),R1
FE69  -  1401   		ADDI	R4,1
FE6A  -  1A01   		SUBI	R2,1
FE6B  -  9FF9   		JNZ		MD2
FE6C  -  5120   		LDI		R1,' '
FE6D  -  69A2   		ST		(R5+UARTDAT-IOBASE),R1
FE6E  -  69A2   		ST		(R5+UARTDAT-IOBASE),R1
FE6F  -         		
FE6F  -  1C08   		SUBI	R4,8
FE70  -  5208   		LDI		R2,8
FE71  -  6080   MD3:	LD		R0,(R4)
FE72  -  5940   		RORI	R1,R0,8
FE73  -  7010   		JAL		GOODCH
FE74  -  69A2   		ST		(R5+UARTDAT-IOBASE),R1
FE75  -  0901   		OR		R1,R0,R0
FE76  -  700D   		JAL		GOODCH
FE77  -  69A2   		ST		(R5+UARTDAT-IOBASE),R1
FE78  -  5120   		LDI		R1,' '
FE79  -  69A2   		ST		(R5+UARTDAT-IOBASE),R1
FE7A  -  1401   		ADDI	R4,1
FE7B  -  1A01   		SUBI	R2,1
FE7C  -  9FF4   		JNZ		MD3
FE7D  -  510A   		LDI		R1,'\n'
FE7E  -  69A2   		ST		(R5+UARTDAT-IOBASE),R1
FE7F  -  1B01   		SUBI	R3,1
FE80  -  9FDD   		JNZ		MD1
FE81  -         		
FE81  -  66E0   		LD		R6,(R7)
FE82  -  1701   		ADDI	R7,1
FE83  -  58FA   		JIND	R6
FE84  -         
FE84  -         ;--------------------------------------
FE84  -         ; 		FORCES A GOOD ASCII CHAR IN R1
FE84  -  31FF   GOODCH:	ANDI	R1,0XFF
FE85  -  497F   		CMPI	R1,127
FE86  -  D002   		JPL		GOCH1
FE87  -  4920   		CMPI	R1,' '
FE88  -  D001   		JPL		GOCH2
FE89  -  512E   GOCH1:	LDI		R1,'.'
FE8A  -  58FA   GOCH2:	JIND	R6
FE8B  -         
FE8B  -         ;-------------------------------------------------------------------
FE8B  -         ; I/O SUBROUTINES
FE8B  -         ;-------------------------------------------------------------------
FE8B  -         
FE8B  -         ;------------------------------------------------------------
FE8B  -         ; PRINTS ASCIIZ (16-BIT PACKED, BIG ENDIAN) STRING ON UART 
FE8B  -         ; PARAMETERS:
FE8B  -         ;	R0: POINTER TO STRING
FE8B  -         ;	R6: RETURN ADDRESS (JAL)
FE8B  -         ; RETURS:
FE8B  -         ; 	R0-R2: MODIFIED
FE8B  -         
FE8B  -  5222   PUTSBE:	LDI		R2,UARTDAT
FE8C  -         PUTSBE1:
FE8C  -  6100   		LD		R1,(R0)
FE8D  -  5944   		RORI	R1,R1,8
FE8E  -  31FF   		ANDI	R1,0XFF
FE8F  -  8007   		JZ		PUTSBE3
FE90  -  6940   		ST		(R2),R1
FE91  -         		
FE91  -  6100   		LD		R1,(R0)
FE92  -  31FF   		ANDI	R1,0XFF
FE93  -  8003   		JZ		PUTSBE3
FE94  -  6940   		ST		(R2),R1
FE95  -  1001   		ADDI	R0,1
FE96  -  FFF5   		JR		PUTSBE1
FE97  -         
FE97  -         PUTSBE3:
FE97  -  58FA   		JIND	R6
FE98  -         ;----------------------------------------------
FE98  -         ; SENDS CHARACTER TO UART
FE98  -         ; PARAMETERS:
FE98  -         ;	R0: CHARACTER TO SEND
FE98  -         ;	R6: RETURN ADDRESS (JAL)
FE98  -         ; RETURNS:
FE98  -         ; 	R1: MODIFFIED
FE98  -         ; NOTES: CPU CLOCK STOPS IF UART NOT READY FOR TRANSMISSION
FE98  -         
FE98  -  5122   PUTCH:	LDI		R1,UARTDAT
FE99  -  6820   		ST		(R1),R0		
FE9A  -  58FA   		JIND	R6			
FE9B  -         
FE9B  -         ;----------------------------------------------
FE9B  -         ; PRINTS DECIMAL NUMBER (OR HEX, OCTAL,...)
FE9B  -         ; PARAMETERS:
FE9B  -         ;	R0: NUMBER TO PRINT
FE9B  -         ;	R6: RETURN ADDRESS (JAL)
FE9B  -          
FE9B  -         ; RETURNS:
FE9B  -         ;	R0, R1: MODIFFIED
FE9B  -         ; NOTES:
FE9B  -         ;	AVOIDS PRINTING ZEROES ON THE LEFT
FE9B  -         ;   STACK USED FOR TEMPORARY DIGIT STORAGE
FE9B  -         
FE9B  -         PRTDEC:
FE9B  -  1F01   		SUBI	R7,1
FE9C  -  6EE0   		ST		(R7),R6
FE9D  -         
FE9D  -  5100   		LDI		R1,0		; END OF STRING MARK
FE9E  -  1F01   		SUBI	R7,1		; TO STACK
FE9F  -  69E0   		ST		(R7),R1
FEA0  -  5100   PRTD0:	LDI		R1,0		; R0=R0/R2, R1=REMAINDER
FEA1  -  5610   		LDI		R6,16		
FEA2  -  0000   PRTD1:	ADD		R0,R0,R0
FEA3  -  0126   		ADC		R1,R1,R1
FEA4  -  190A   		SUBI	R1,10
FEA5  -  B002   		JNC		PRTD2
FEA6  -  1001   		ADDI	R0,1
FEA7  -  F001   		JR		PRTD5
FEA8  -  110A   PRTD2:	ADDI	R1,10
FEA9  -  1E01   PRTD5:	SUBI	R6,1
FEAA  -  9FF7   		JNZ		PRTD1
FEAB  -  490A   		CMPI	R1,10		; DIGITS OVER '9' TRANSLATED TO 'A','B'...
FEAC  -  C001   		JMI		PRTD6
FEAD  -  1107   		ADDI	R1,7
FEAE  -  5630   PRTD6:	LDI		R6,'0'		; REMAINDER IN ASCII GOES TO STACK
FEAF  -  0138   		ADD		R1,R1,R6
FEB0  -  1F01   		SUBI	R7,1
FEB1  -  69E0   		ST		(R7),R1
FEB2  -  0801   		OR		R0,R0,R0	; REPEAT UNTIL ZERO
FEB3  -  9FEC   		JNZ		PRTD0
FEB4  -         
FEB4  -  60E0   PRTD3:	LD		R0,(R7)		; RETRIEVE CHARACTER FROM STACK
FEB5  -  8003   		JZ		PRTD9		; END OF STRING?
FEB6  -  1701   		ADDI	R7,1
FEB7  -  7FE0   		JAL		PUTCH
FEB8  -  FFFB   		JR		PRTD3
FEB9  -         
FEB9  -  1701   PRTD9:	ADDI	R7,1
FEBA  -  66E0   		LD		R6,(R7)
FEBB  -  1701   		ADDI	R7,1
FEBC  -  58FA   		JIND	R6
FEBD  -         
FEBD  -         ;-------------------------------------------
FEBD  -         ; PRINT LOWER 8 BITS OF R0 AS HEX (2 DIGITS)
FEBD  -         PRTHEX8:
FEBD  -  1F02   		SUBI	R7,2
FEBE  -  6AE0   		ST		(R7),R2
FEBF  -  6EE1   		ST		(R7+1),R6
FEC0  -  5622   		LDI		R6,UARTDAT
FEC1  -  5202   		LDI		R2,2
FEC2  -  5840   		RORI	R0,R0,8
FEC3  -  F005   		JR		PTX1
FEC4  -         
FEC4  -         ;--------------------------------------
FEC4  -         ; PRINT 16 BITS OF R0 AS HEX (4 DIGITS)
FEC4  -  1F02   PRTHEX:	SUBI	R7,2
FEC5  -  6AE0   		ST		(R7),R2
FEC6  -  6EE1   		ST		(R7+1),R6
FEC7  -  5622   		LDI		R6,UARTDAT
FEC8  -  5204   		LDI		R2,4
FEC9  -  5860   PTX1:	RORI	R0,R0,12
FECA  -  0901   		OR		R1,R0,R0
FECB  -  310F   		ANDI	R1,15
FECC  -  490A   		CMPI	R1,10
FECD  -  C001   		JMI		PTX2
FECE  -  1107   		ADDI	R1,7
FECF  -  1130   PTX2:	ADDI	R1,'0'
FED0  -  69C0   		ST		(R6),R1
FED1  -  1A01   		SUBI	R2,1
FED2  -  9FF6   		JNZ		PTX1
FED3  -  62E0   		LD		R2,(R7)
FED4  -  66E1   		LD		R6,(R7+1)
FED5  -  1702   		ADDI	R7,2
FED6  -  58FA   		JIND	R6
FED7  -         
FED7  -         ;-------------------------------------------------------------------
FED7  -         ; VARIABLES
FED7  -         ;-------------------------------------------------------------------
FED7  -         BVAR:
FED7  -  0000   LDPCF:	WORD	0
FED8  -  0000   TRACE:	WORD	0
FED9  -  0000   BREAK:	WORD 	0
FEDA  -  0000   RUTSP:	WORD	0
FEDB  -  0100   CADDR:	WORD	0X100
FEDC  -         
FEDC  -         ;-------------------------------------------------------------------
FEDC  -         ; TEXT (OVERWRITABLE BY THE STACK...MAYBE)
FEDC  -         ;-------------------------------------------------------------------
FEDC  -  0A3C   MSGHELP:	ASCZBE "\n<space>\tredraw screen\nc\tContinue (no stop)\ne\tExecute (w breakpoint)\ns\tSingle step\nn\texec until Next instr\nr\texec until return (JIND R6)\nR\texec until return at higher stack level\nb [adr]\tset Breakpoint\nd [adr]\tDissasemble\nm [adr]\tMemory dump\n"
FEDD  -  7370   
FEDE  -  6163   
FEDF  -  653E   
FEE0  -  0972   
FEE1  -  6564   
FEE2  -  7261   
FEE3  -  7720   
FEE4  -  7363   
FEE5  -  7265   
FEE6  -  656E   
FEE7  -  0A63   
FEE8  -  0943   
FEE9  -  6F6E   
FEEA  -  7469   
FEEB  -  6E75   
FEEC  -  6520   
FEED  -  286E   
FEEE  -  6F20   
FEEF  -  7374   
FEF0  -  6F70   
FEF1  -  290A   
FEF2  -  6509   
FEF3  -  4578   
FEF4  -  6563   
FEF5  -  7574   
FEF6  -  6520   
FEF7  -  2877   
FEF8  -  2062   
FEF9  -  7265   
FEFA  -  616B   
FEFB  -  706F   
FEFC  -  696E   
FEFD  -  7429   
FEFE  -  0A73   
FEFF  -  0953   
FF00  -  696E   
FF01  -  676C   
FF02  -  6520   
FF03  -  7374   
FF04  -  6570   
FF05  -  0A6E   
FF06  -  0965   
FF07  -  7865   
FF08  -  6320   
FF09  -  756E   
FF0A  -  7469   
FF0B  -  6C20   
FF0C  -  4E65   
FF0D  -  7874   
FF0E  -  2069   
FF0F  -  6E73   
FF10  -  7472   
FF11  -  0A72   
FF12  -  0965   
FF13  -  7865   
FF14  -  6320   
FF15  -  756E   
FF16  -  7469   
FF17  -  6C20   
FF18  -  7265   
FF19  -  7475   
FF1A  -  726E   
FF1B  -  2028   
FF1C  -  4A49   
FF1D  -  4E44   
FF1E  -  2052   
FF1F  -  3629   
FF20  -  0A52   
FF21  -  0965   
FF22  -  7865   
FF23  -  6320   
FF24  -  756E   
FF25  -  7469   
FF26  -  6C20   
FF27  -  7265   
FF28  -  7475   
FF29  -  726E   
FF2A  -  2061   
FF2B  -  7420   
FF2C  -  6869   
FF2D  -  6768   
FF2E  -  6572   
FF2F  -  2073   
FF30  -  7461   
FF31  -  636B   
FF32  -  206C   
FF33  -  6576   
FF34  -  656C   
FF35  -  0A62   
FF36  -  205B   
FF37  -  6164   
FF38  -  725D   
FF39  -  0973   
FF3A  -  6574   
FF3B  -  2042   
FF3C  -  7265   
FF3D  -  616B   
FF3E  -  706F   
FF3F  -  696E   
FF40  -  740A   
FF41  -  6420   
FF42  -  5B61   
FF43  -  6472   
FF44  -  5D09   
FF45  -  4469   
FF46  -  7373   
FF47  -  6173   
FF48  -  656D   
FF49  -  626C   
FF4A  -  650A   
FF4B  -  6D20   
FF4C  -  5B61   
FF4D  -  6472   
FF4E  -  5D09   
FF4F  -  4D65   
FF50  -  6D6F   
FF51  -  7279   
FF52  -  2064   
FF53  -  756D   
FF54  -  700A   
FF55  -  0000   
FF56  -  0A55   TXTLOAD:	ASCZBE "\nUpload program to debug\n"
FF57  -  706C   
FF58  -  6F61   
FF59  -  6420   
FF5A  -  7072   
FF5B  -  6F67   
FF5C  -  7261   
FF5D  -  6D20   
FF5E  -  746F   
FF5F  -  2064   
FF60  -  6562   
FF61  -  7567   
FF62  -  0A00   
FF63  -         ;-------------------------------------------------------------------
FF63  -         ;
FF63  -         ;		MAIN (OVERWRITABLE BY THE STACK)
FF63  -         ;
FF63  -         ;-------------------------------------------------------------------
FF63  -         PSTART:	
FF63  -         START:	
FF63  -  5700   		LDI		R7,0
FF64  -         
FF64  -  58E0   		LDPC	R0
FF65  -  FF56   		WORD	TXTLOAD
FF66  -  7F24   		JAL		PUTSBE
FF67  -         
FF67  -         		; BOOTLOADER-LIKE CODE
FF67  -  5520   		LDI		R5,IOBASE
FF68  -  60A2   BUC1:	LD		R0,(R5+UARTDAT-IOBASE)
FF69  -  484C   		CMPI	R0,'L'
FF6A  -  9FFD   		JNZ		BUC1
FF6B  -  68A2   		ST		(R5+UARTDAT-IOBASE),R0
FF6C  -  7018   		JAL		GETW
FF6D  -  0A01   		OR		R2,R0,R0		; ADDRESS
FF6E  -  7016   		JAL		GETW
FF6F  -  0B01   		OR		R3,R0,R0		; COUNT
FF70  -  7014   		JAL		GETW
FF71  -  0C01   		OR		R4,R0,R0		; ENTRY POINT
FF72  -         
FF72  -  7012   BUC2:	JAL		GETW
FF73  -  6840   		ST		(R2),R0
FF74  -  1201   		ADDI	R2,1
FF75  -  1B01   		SUBI	R3,1
FF76  -  9FFB   		JNZ		BUC2
FF77  -         
FF77  -         		; SET IRQ JUMP 
FF77  -  51FF   		LDI		R1,0XFF
FF78  -  58E0   		LDPC	R0
FF79  -  FAEF   		WORD	JMPIRQ
FF7A  -  6831   		ST		(R1+0X110-0XFF),R0
FF7B  -         		; SET TRACE, BREAK, CLEAR DIRTY
FF7B  -  5AE0   		LDPC	R2
FF7C  -  FED7   		WORD	BVAR
FF7D  -  5000   		LDI		R0,0
FF7E  -  6941   		ST		(R2+TRACE-BVAR),R1
FF7F  -  6842   		ST		(R2+BREAK-BVAR),R0
FF80  -  68A6   		ST		(R5+FLAG0-IOBASE),R0
FF81  -         
FF81  -         		; ENABLE SSTEP IRQ
FF81  -  60A0   		LD		R0,(R5+IRQEN-IOBASE)
FF82  -  3810   		ORI		R0,0X10
FF83  -  68A0   		ST		(R5+IRQEN-IOBASE),R0
FF84  -         		; AND JUMP TO CODE
FF84  -  58F2   		JIND	R4
FF85  -         
FF85  -         ;-------------------------------------------------------------------
FF85  -         ; GET 16-BIT WORD FROM UART (TWO BYTES)
FF85  -  60A2   GETW:	LD		R0,(R5+UARTDAT-IOBASE)	; LSB
FF86  -  61A2   		LD		R1,(R5+UARTDAT-IOBASE)	; MSB
FF87  -  5944   		RORI	R1,R1,8
FF88  -  0805   		OR		R0,R0,R1
FF89  -  58FA   		JIND	R6
FF8A  -         
FF8A  -         
FF8A  -         PEND:
FF8A  -         
FF8A  -         
FF8A  -         
